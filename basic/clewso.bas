1000    DIM P(5, 9), S$(5), R$(6), X(10)
1010    RANDOMIZE TIMER
1015    CLS
1020    REM*****************************************************
1030    REM*                                                   *
1040    REM*     INSPECTOR CLEWSO                              *
1050    REM*     BY:RON CARLSON                                *
1060    REM*     FEB. 1979                                     *
1070    REM*                                                   *
1080    REM*****************************************************
1090    FOR I = 1 TO 5
1100        READ S$(I)
1110    NEXT I
1120    DATA "BILL","MARY","PAUL","SUZY","JOHN"
1130    FOR I = 1 TO 6
1140        READ R$(I)
1150    NEXT I
1160    DATA "LOUNGE","ATRIUM","LIVING","DINING","TROPHY","GARAGE"
1170    PRINT "BILL, MARY, JOHN, SUZY AND PAUL ARE HOUSE GUESTS.  THEIR HOST "
1180    PRINT "WAS MURDERED BY ONE OF THEM BETWEEN 1 PM. AND 9 PM."
1190    PRINT "YOUR JOB AS INSPECTOR CLEWSO, IS TO FIND THE KILLER, TIME AND"
1200    PRINT "ROOM.  YOU WILL BE GIVEN A HOUSE DIAGRAM AND A SET OF QUESTIONS"
1210    PRINT "FOR THE SUSPECTS, BUT THE GUILTY PERSON MAY TRY TO MISLEAD YOU,"
1220    PRINT "SOME OF THE TIME."
1230    PRINT "IF ONE OF THE SUSPECTS CLAIM THAT THE HOST WAS ALREADY DEAD,"
1240    PRINT "OR THAT THE HOST WAS STILL ALIVE, THEN YOU HAVE FOUND THE "
1250    PRINT "ROOM WHERE THE MURDER TOOK PLACE."
1260    PRINT
1270    H = 0
1280    C = 0
1290    C1 = 0
1300    PRINT "   !-----------------------------!"
1310    PRINT "   !         !         O GARAGE  !"
1320    PRINT "   !         O TROPHY  !         !"
1330    PRINT "   ! DINING  !---!--  -!  --     !"
1340    PRINT "   !                       !-----! NO WINDOWS"
1350    PRINT "   !--O----------!        -!     !"
1360    PRINT "   !                             !---"
1370    PRINT "   !             !--  -------       !"
1380    PRINT "   ! LIVING   ---!         !        !"
1390    PRINT "   !--        !            O LOUNGE !"
1400    PRINT "     !        0  ATRIUM    !    ----!"
1410    PRINT "     !--------!------------!----!"
1420    FOR K = 1 TO 5
1430    P(K, 1) = INT(6 * RND(K)) + 1
1440    NEXT K
1450    FOR L = 2 TO 9
1460    FOR K = 1 TO 5
1470    A = INT(6 * RND(K)) + 1
1480    IF A = P(K, L - 1) THEN 1470
1490    P(K, L) = A
1500    NEXT K
1510    NEXT L
1520    REM ESTABLISHED SUSPECT'S MOVEMENTS
1530    REM RANDOM ASSIGNMENT OF KILLER, TIME AND ROOM
1540    M = INT(5 * RND(1)) + 1
1550    T = INT(9 * RND(2)) + 1
1560    R = P(M, T)
1570    INPUT "INSPECTOR CLEW-SO WHO IS YOUR SUSPECT(BILL,MARY,PAUL,SUZY or JOHN,QUIT) "; S1$
1585    IF LEFT$(S1$, 1) = "Q" THEN 2980
1590    FOR S = 1 TO 5
1600        IF S1$ = S$(S) THEN 1630
1610    NEXT S
1620    S = 0
1630    IF S = 0 THEN 1570
1640    PRINT
1650    PRINT "DO YOU WISH TO QUESTION "; S1$; " ABOUT :"
1660    PRINT "   1   - THE SUSPECTS WHEREABOUTS AT A PARTICULAR TIME"
1670    PRINT "   2   - WHAT TIME THE SUSPECT WAS IN A CERTAIN ROOM"
1680    PRINT "   3   - THE CRIME IS SOLVED ---";
1690    INPUT A1$
1700    A = VAL(A1$)
1710    REM A = x(1) - 48
1720    IF A < 1 THEN 1650
1730    IF A > 3 THEN 1650
1740    C = C + 1
1750    ON A GOTO 1760, 2120, 2380
1760    PRINT S1$;
1770    REM TIME SECTION
1780    PRINT " WHERE WERE YOU AT (1-9) ";
1790    INPUT T1$
1800    T1 = VAL(T1$)
1810    REM T1 = X(1) - 48
1820    IF T1 < 1 THEN 1760
1830    IF T1 > 9 THEN 1760
1840    R1 = P(S, T1)
1850    IF S <> M THEN 1940
1860    REM LIAR SECTION
1870    IF RND(2) < .5 THEN 2020
1880    R1 = INT(6 * RND(3)) + 1
1890    IF RND(4) < .5 THEN 1920
1900    PRINT "THE HOST WAS ALREADY DEAD."
1910    GOTO 2020
1920    PRINT "OUR HOST WAS STILL ALIVE."
1930    GOTO 2020
1940    IF R1 = R THEN 1960
1950    GOTO 2020
1960    IF RND(5) < .5 THEN 2020
1970    IF T1 >= T THEN 2000
1980    PRINT " THE HOST WAS STILL ALIVE."
1990    GOTO 2020
2000    IF T1 <= T THEN 2020
2010    PRINT " THE HOST WAS ALREADY DEAD."
2020    PRINT "I WAS IN THE "; R$(R1); " ROOM."
2030    FOR K = 1 TO 5
2040    IF K = S THEN 2090
2050    IF P(K, T1) <> R1 THEN 2070
2060    PRINT "I WAS WITH "; S$(K)
2070    IF ABS(R1 - P(K, T1)) <> 1 THEN 2090
2080    PRINT "I SAW "; S$(K)
2090    NEXT K
2100    GOTO 1570
2110    REM ROOM QUESTIONING
2120    PRINT S1$;
2130    PRINT " WHAT TIME WERE YOU IN [ROOM] ";
2140    INPUT R1$
2150    FOR T1 = 1 TO 6
2160        IF R1$ = R$(T1) THEN 2190
2170    NEXT T1
2180    GOTO 2120
2190    IF S <> M THEN 2280
2200    IF RND(5) < .5 THEN 2230
2210    T1 = INT(RND(5) * 9) + 1
2220    GOTO 2260
2230    IF T1 <> T THEN 2260
2240    PRINT "I WAS NOT IN THAT ROOM."
2250    GOTO 1570
2260    PRINT "I WAS IN THAT ROOM AT "; T1
2270    GOTO 1570
2280    K = 0
2290    FOR B = 1 TO 9
2300    IF P(S, B) <> T1 THEN 2330
2310    PRINT "I WAS IN THAT ROOM AT "; B
2320    K = 1
2330    NEXT B
2340    IF K = 1 THEN 1570
2350    PRINT "I WAS NOT IN THAT ROOM."
2360    GOTO 1570
2370    REM COMFRONTATION SECTION
2380    C1 = C1 + 1
2390    PRINT "INSPECTOR DO YOU KNOW :"
2400    PRINT "  1   KILLER"
2410    PRINT "  2   ROOM"
2420    PRINT "  3   TIME"
2430    PRINT "  4   TOTALLY BAFFLED ---";
2440    INPUT A1$
2450    A = VAL(A1$)
2470    IF A <= 0 GOTO 2390
2480    IF A > 4 GOTO 2390
2490    ON A GOTO 2500, 2730, 2620, 2900
2500    PRINT " THE KILLER IS  ";
2510    INPUT S1$
2520    FOR X = 1 TO 5
2530        IF S1$ = S$(X) THEN 2560
2540    NEXT X
2550    X = 0
2560    IF X = 0 THEN 2390
2570    IF S1$ <> S$(M) THEN 2880
2580    PRINT "YOU HAVE THE KILLER, INSPECTOR CLEW-SO."
2590    H = H + 1
2600    IF H = 3 THEN 2850
2610    GOTO 1570
2620    PRINT "TIME OF THE MURDER ";
2630    INPUT T1$
2640    T1 = VAL(T1$)
2660    IF T1 < 1 THEN 2620
2670    IF T1 > 9 THEN 2620
2680    IF T1 <> T THEN 2880
2690    PRINT "INSPECTOR YOU HAVE THE RIGHT TIME."
2700    H = H + 1
2710    IF H = 3 THEN 2850
2720    GOTO 1570
2730    PRINT "ROOM OF THE MURDER ";
2740    INPUT R1$
2750    FOR X = 1 TO 6
2760        IF R1$ = R$(X) THEN 2790
2770    NEXT X
2780    X = 0
2790    IF X = 0 THEN 2730
2800    IF R1$ <> R$(R) THEN 2880
2810    PRINT "INSPECTOR , YOU NOW HAVE THE ROOM."
2820    H = H + 1
2830    IF H <> 3 THEN 1570
2840    REM CONFIRMATIONS
2850    PRINT "YOU ARE BRILLIANT INSPECTOR CLEW-SO."
2860    PRINT "IT TOOK YOU "; C; " QUESTIONS AND "; C1; "CONFRONTATIONS."
2870    GOTO 2940
2880    PRINT "INSPECTOR CLEW-SO YOU ARE A BUMBLING IDIOT.TRY AGAIN"
2890    GOTO 1570
2900    PRINT "TOO BAD INSPECTOR CLEW-SO"
2910    PRINT " THE FACTS ARE:"
2920    PRINT S$(M); " KILLED THE HOST AT "; T; " O'CLOCK IN THE ";
2930    PRINT R$(R); " (ROOM)"
2940    PRINT "DO YOU WANT A NEW CASE INSPECTOR  ";
2950    INPUT A$
2960    IF A$ = "YES" THEN 1170
2970    IF A$ = "Y" THEN 1170
2980    END

